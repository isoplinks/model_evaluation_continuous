$PROB WARFARIN PKPD
;
;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551
;
$INPUT ID TIME WT AGE SEX DVID DV MDV EVID CMT AMT
$DATA ..\ka1_to_emax1_simln.csv
IGNORE (DVID.EQ.2)

$EST MAX=9990 NSIG=3 SIGL=9 PRINT=1
METHOD=COND INTER NOABORT

$THETA
(0.01,0.131,1) ; POP_CL L/H/70KG
(0.01,8.1,20) ; POP_V L/70KG
(0.01,0.937,24) ; POP_TABS H
(0.01,0.946,24) ; POP_LAG H
$OMEGA
0.0657 ; PPV_CL
0.0192 ; PPV_V
0.449 ; PPV_TABS
0.473 ; PPV_LAG
$SIGMA 0.00468 ; RUV_CV
$SIGMA 0.136 ; RUV_SD MG/L

$SUBR ADVAN2 TRAN2

$PK

   IF (NEWIND.EQ.0) LN2=LOG(2)

   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
   TABS=THETA(3)*EXP(ETA(3))
   TLAG=THETA(4)*EXP(ETA(4))

   KA=LN2/TABS
   ALAG1=TLAG
   S2=V

$ERROR

   CP=A(2)/S2
   Y=CP*(1+ERR(1)) + ERR(2)

$TABLE ID TIME DVID Y
ONEHEADER NOPRINT FILE=ka1.fit



