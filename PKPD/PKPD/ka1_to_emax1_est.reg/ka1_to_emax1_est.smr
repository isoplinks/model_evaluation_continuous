THETA:      POP_CL      POP_V       POP_TABS    POP_LAG     POP_E0      POP_EMAX    POP_C50     POP_TEQ     
ETA:        PPV_CL      PPV_V       PPV_TABS    PPV_LAG     PPV_E0      PPV_EMAX    PPV_C50     PPV_TEQ     
ERR:        RUV_CV      RUV_SD      RUV_FX      
ka1_to_emax1_est.lst	1279.342	FOCEI	eval=177 sig=2.9 sub=32 obs=483 NM7.3.0 PP7.3.0 
THETA     = 0.131       8.06        0.944       1.03        95.9        -1          1.17        13.3

ETASD     = 0.255c      0.142c      0.669c      0.558c      0.045       0.000c      0.369       0.120

ETAPval   = 0.953       0.844       0.424       0.327       0.823       1.000       0.966       0.997
ETAshr%   = 0.3         6.7         48.7        65.3        23.8        100.0       7.2         22.6
EBVshr%   = 2.1         10.4        43.2        49.6        25.1        0.0         8.0         23.4
EPSshr%   = 15.0        0.0         15.8

EPSSD     = 0.069c      0.367c      3.728

MINIMIZATION TERMINATED
DUE TO ROUNDING ERRORS (ERROR=134)
ADDITIONAL PROBLEMS OCCURRED WITH THE MINIMIZATION.
REGARD THE RESULTS OF THE ESTIMATION STEP CAREFULLY, AND ACCEPT THEM ONLY
AFTER CHECKING THAT THE COVARIANCE STEP PRODUCES REASONABLE OUTPUT.

 Ttot 2:24.45 Test 2:18.38 Tcov 0:0 Ttcl 0:6.07

$PROB WARFARIN PKPD

;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551

$INPUT ID TIME WT AGE SEX DVID DV MDV EVID CMTX AMT
$DATA ..\ka1_to_emax1_simln.csv

$EST MAX=9990 NSIG=3 SIGL=9 PRINT=1
METHOD=COND INTER NOABORT
$COV

$THETA
(0.01,0.131,1) FIX ; POP_CL L/H/70KG
(0.01,8.06,20) FIX ; POP_V L/70KG
(0.01,0.944,24) FIX ; POP_TABS H
(0.01,1.03,24) FIX ; POP_LAG H
$OMEGA
0.0649 FIX ; PPV_CL
0.0201 FIX ; PPV_V
0.447 FIX ; PPV_TABS
0.311 FIX ; PPV_LAG
$SIGMA 0.00478 FIX ; RUV_CV
$SIGMA 0.135 FIX ; RUV_SD MG/L

$THETA
(0.01,95.9,200) ; POP_E0
-1. FIX ; POP_EMAX
(0.01,1.17,10) ; POP_C50
(0.01,13.3,100) ; POP_TEQ
$OMEGA
0.00199 ; PPV_E0
0 FIX ; PPV_EMAX
0.136 ; PPV_C50
0.0143 ; PPV_TEQ
$SIGMA
13.9 ; RUV_FX

$SUBR ADVAN13 TOL=9
$MODEL
   COMP=GUT
   COMP=CENTRAL
   COMP=PCA
$PK

   IF (NEWIND.EQ.0) LN2=LOG(2)

   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
   TABS=THETA(3)*EXP(ETA(3))
   TLAG=THETA(4)*EXP(ETA(4))

   E0=THETA(5)*EXP(ETA(5))
   EMAX=THETA(6)*EXP(ETA(6))
   C50=THETA(7)*EXP(ETA(7))
   TEQ=THETA(8)*EXP(ETA(8))

   KPCA=LN2/TEQ
   RPCA=E0*KPCA

   KA=LN2/TABS
   ALAG1=TLAG
   S2=V

   A_0(3)=E0
 
$DES
   RATEIN=KA*A(1)
   DCP=A(2)/S2
   DPCA=A(3)
   PD=1+EMAX*DCP/(C50+DCP)

   DADT(1)=-RATEIN
   DADT(2)=RATEIN - CL*DCP
   DADT(3)=RPCA*PD - KPCA*DPCA

$ERROR

   CP=A(2)/S2
   PCA=A(3)
   IF (DVID.LE.1) THEN
      Y=CP*(1+ERR(1)) + ERR(2)
   ENDIF
   IF (DVID.EQ.2) THEN
      Y=PCA + ERR(3)
   ENDIF
   CE=CP

$TABLE ID TIME DVID CP CE PCA Y
ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 ETA8
ONEHEADER NOPRINT FILE=ka1_to_emax1_est.fit


