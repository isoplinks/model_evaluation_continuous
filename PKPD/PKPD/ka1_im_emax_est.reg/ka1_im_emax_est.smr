THETA:      POP_CL      POP_V       POP_TABS    POP_LAG     POP_E0      POP_EMAX    POP_C50     
ETA:        PPV_CL      PPV_V       PPV_TABS    PPV_LAG     PPV_E0      PPV_EMAX    PPV_C50     
ERR:        RUV_CV      RUV_SD      RUV_FX      
ka1_im_emax_est.lst	1400.761	FOCEI	eval=854 sig=NA sub=32 obs=483 NM7.3.0 PP7.3.0 
THETA     = 0.131       8.06        0.944       1.03        96.2        -73.5       0.162

ETASD     = 0.255c      0.142c      0.669c      0.558c      0.016       0.032       1.077

ETAPval   = 0.984       0.988       0.344       0.966       0.606       0.391       0.399
ETAshr%   = 0.0         10.0        48.6        58.4        65.1        54.1        30.8
EBVshr%   = 2.2         11.1        42.8        49.6        64.8        53.4        30.1
EPSshr%   = 16.1        15.4        6.1

EPSSD     = 0.069c      0.367c      7.477

MINIMIZATION TERMINATED
DUE TO ROUNDING ERRORS (ERROR=134)

 Ttot 0:37.9 Test 0:34.11 Tcov 0:0 Ttcl 0:3.79

$PROB WARFARIN PKPD

;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551

$INPUT ID TIME WT AGE SEX DVID DV MDV EVID CMTX AMT
$DATA ..\ka1_to_emax1_simln.csv

$EST MAX=9990 NSIG=3 SIGL=9 PRINT=1
METHOD=COND INTER NOABORT
$COV

$THETA
(0.01,0.131,1) FIX ; POP_CL L/H/70KG
(0.01,8.06,20) FIX ; POP_V L/70KG
(0.01,0.944,24) FIX ; POP_TABS H
(0.01,1.03,24) FIX ; POP_LAG H
$OMEGA
0.0649 FIX ; PPV_CL
0.0201 FIX ; PPV_V
0.447 FIX ; PPV_TABS
0.311 FIX ; PPV_LAG
$SIGMA 0.00478 FIX ; RUV_CV
$SIGMA 0.135 FIX ; RUV_SD MG/L

$THETA
(0.01,96.2,200) ; POP_E0
(-INF,-73.5,0) ; POP_EMAX
(0.01,0.164,100) ; POP_C50
$OMEGA
0.00025 ; PPV_E0
0.00101 ; PPV_EMAX
1.15 ; PPV_C50
$SIGMA 56.1 ; RUV_FX

$SUBR ADVAN2 TRAN2

$PK

   IF (NEWIND.EQ.0) LN2=LOG(2)

   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
   TABS=THETA(3)*EXP(ETA(3))
   TLAG=THETA(4)*EXP(ETA(4))

   KA=LN2/TABS
   ALAG1=TLAG
   S2=V

   E0=THETA(5)*EXP(ETA(5))
   EMAX=THETA(6)*EXP(ETA(6))
   C50=THETA(7)*EXP(ETA(7))

   DLTA=E0+EMAX

$ERROR

   CP=A(2)/S2
   PCA=E0 + EMAX*CP/(C50+CP)
   IF (DVID.LE.1) THEN
      Y=CP*(1+ERR(1)) + ERR(2)
   ENDIF
   IF (DVID.EQ.2) THEN
      Y=PCA + ERR(3)
   ENDIF

   CE=CP

$TABLE ID TIME DVID CP CE PCA Y
ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7
ONEHEADER NOPRINT FILE=ka1_im_emax_est.fit



