$PROB WARFARIN PKPD

;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551

$INPUT ID TIME WT AGE SEX DVID DV MDV EVID CMTX AMT
$DATA ..\ka1_to_emax1_simln.csv

;$EST MAX=9990 NSIG=3 SIGL=9 PRINT=1
;METHOD=COND INTER NOABORT
;$COV

$THETA
(0.01,0.131,1) FIX ; POP_CL L/H/70KG
(0.01,8.06,20) FIX ; POP_V L/70KG
(0.01,0.944,24) FIX ; POP_TABS H
(0.01,1.03,24) FIX ; POP_LAG H
$OMEGA
0.0649 FIX ; PPV_CL
0.0201 FIX ; PPV_V
0.447 FIX ; PPV_TABS
0.311 FIX ; PPV_LAG
$SIGMA 0.00478 FIX ; RUV_CV
$SIGMA 0.135 FIX ; RUV_SD MG/L

$THETA
(0.01,96.2,200) ; POP_E0
(-INF,-73.5,0) ; POP_EMAX
(0.01,0.164,100) ; POP_C50
$OMEGA
0.00025 ; PPV_E0
0.00101 ; PPV_EMAX
1.15 ; PPV_C50
$SIGMA 56.1 ; RUV_FX

$SUBR ADVAN2 TRAN2

$PK
OBS=DV
   IF (NEWIND.EQ.0) LN2=LOG(2)

   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
   TABS=THETA(3)*EXP(ETA(3))
   TLAG=THETA(4)*EXP(ETA(4))

   KA=LN2/TABS
   ALAG1=TLAG
   S2=V

   E0=THETA(5)*EXP(ETA(5))
   EMAX=THETA(6)*EXP(ETA(6))
   C50=THETA(7)*EXP(ETA(7))

   DLTA=E0+EMAX

$ERROR

   CP=A(2)/S2
   PCA=E0 + EMAX*CP/(C50+CP)
   IF (DVID.LE.1) THEN
      Y=CP*(1+ERR(1)) + ERR(2)
   ENDIF
   IF (DVID.EQ.2) THEN
      Y=PCA + ERR(3)
   ENDIF

   CE=CP

; SIMULATION START
REP=IREP
$TABLE REP ID TIME DV PRED OBS MDV DVID WT AGE SEX
NOAPPEND ONEHEADER NOPRINT FILE=ka1_im_emax_est.fit
$SIM (20150330) ONLYSIM NSUB=100
; SIMULATION END
