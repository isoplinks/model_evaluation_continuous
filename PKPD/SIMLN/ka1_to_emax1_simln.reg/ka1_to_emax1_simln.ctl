$PROB WARFARIN PKPD

;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551

$INPUT ID TIME WT AGE SEX DVID DV MDV EVID CMT AMT
$DATA ..\ka1_to_emax1_simln.csv

$SIM (20080422) ONLYSIM NSUB=1

;$EST MAX=0 SIG=3 PRINT=1
;METHOD=COND INTER NOABORT

$THETA
(0.01,0.135,1) ; POP_CL L/H/70KG
(0.01,8.03,20) ; POP_V L/70KG
(0.01,0.588,24) ; POP_TABS H
(0.01,0.842,24) ; POP_LAG H
(0.01,96.6,200) ; POP_E0
-1 FIX ; POP_EMAX
(0.01,1.17,10) ; POP_C50
(0.01,13.,100) ; POP_TEQ

$OMEGA
0.0703 ; PPV_CL
0.0195 ; PPV_V
0.551 ; PPV_TABS
0.347 ; PPV_LAG
0.00286 ; PPV_E0
0 FIX ; PPV_EMAX
0.2 ; PPV_C50
0.0114 ; PPV_TEQ

$SIGMA
0.00677 ; RUV_CV
0.094 ; RUV_SD MG/L
14.6 ; RUV_FX

$SUBR ADVAN6 TOL=3
$MODEL
   COMP=GUT
   COMP=CENTRAL
   COMP=PCA
$PK

   IF (NEWIND.EQ.0) LN2=LOG(2)

   FSZV=WT/70
   FSZCL=FSZV**0.75

   CL=FSZCL*THETA(1)*EXP(ETA(1))
   V=FSZV*THETA(2)*EXP(ETA(2))
   TABS=THETA(3)*EXP(ETA(3))
   TLAG=THETA(4)*EXP(ETA(4))

   E0=THETA(5)*EXP(ETA(5))
   EMAX=THETA(6)*EXP(ETA(6))
   C50=THETA(7)*EXP(ETA(7))
   TEQ=THETA(8)*EXP(ETA(8))
   KPCA=LN2/TEQ
   RPCA=E0*KPCA

   KA=LN2/TABS
   ALAG1=TLAG
   S2=V
   A_0(3)=E0

 
$DES
   RATEIN=KA*A(1)
   DCP=A(2)/S2
   DPCA=A(3)
   PD=1+EMAX*DCP/(C50+DCP)

   DADT(1)=-RATEIN
   DADT(2)=RATEIN - CL*DCP
   DADT(3)=RPCA*PD - KPCA*DPCA

$ERROR

   CP=A(2)/S2
   PCA=A(3)
   IF (DVID.LE.1) THEN
      Y=CP*(1+ERR(1)) + ERR(2)
   ENDIF
   IF (DVID.EQ.2) THEN
      Y=PCA + ERR(3)
   ENDIF

$TABLE ID TIME WT AGE SEX DVID DV MDV EVID CMT AMT
NOAPPEND ONEHEADER NOPRINT FILE=ka1_to_emax1_simln.fit



